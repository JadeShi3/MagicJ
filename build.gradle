// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.versions = [
            'compileSdk'         : 28,
            'minSdk'             : 14,
            'targetSdk'          : 28,
            'sourceCompatibility': JavaVersion.VERSION_1_7,
            'targetCompatibility': JavaVersion.VERSION_1_7,
            'okhttp'             : '3.10.0',
            'supportLibrary'     : '28.0.0-alpha3'
    ]

    ext.deps = [
            androidPlugin       : 'com.android.tools.build:gradle:3.2.0-beta02',
            okhttp              : "com.squareup.okhttp3:okhttp:${versions.okhttp}",
            mockWebServer       : "com.squareup.okhttp3:mockwebserver:${versions.okhttp}",
            pollexor            : 'com.squareup:pollexor:2.0.4',
            supportCompat       : "com.android.support:support-compat:${versions.supportLibrary}",
            supportFragment     : "com.android.support:support-fragment:${versions.supportLibrary}",
            supportAnnotations  : "com.android.support:support-annotations:${versions.supportLibrary}",
            supportExifInterface: "com.android.support:exifinterface:${versions.supportLibrary}",
            errorProne          : 'com.google.errorprone:error_prone_core:2.3.1',
            junit               : 'junit:junit:4.12',
            truth               : 'com.google.truth:truth:0.41',
            robolectric         : 'org.robolectric:robolectric:4.0-alpha-2',
            mockito             : 'org.mockito:mockito-core:2.19.0'
    ]

    ext.isCi = "true" == System.getenv('CI')


    repositories {
        google()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath deps.androidPlugin
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.14'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply plugin: 'com.github.ben-manes.versions'

subprojects {
    repositories {
        google()
        jcenter()
    }

    tasks.withType(Test) {
        testLogging {
            events "failed"
            exceptionFormat "full"
            showExceptions true
            showStackTraces true
            showCauses true
        }
    }

    apply plugin: 'net.ltgt.errorprone'

    dependencies {
        errorprone deps.errorProne
    }
}